{-# OPTIONS --safe --without-K #-}
module Omniscience.Implications where

open import Prelude
open import Theorems.Constructive
open import Omniscience.Principles

{- The various principles in Omniscience.Principles are related as follows:

        LEM  â‡”  WLEM  Ã—  DNE    (i.e. LEM â†’ WLEM, LEM â†’ DNE, and WLEM â†’ DNE â†’ LEM)
         â‡•       â‡•       â‡•      (i.e. LEM â†’ LPO, LPO â†’ LEM, WLEM â†’ WLPO, WLPO â†’ WLEM, etc.) 
        LPO  â‡”  WLPO  Ã—  MP
         â†“       â†“        â†“
       B.LPO â‡” B.WLPO Ã— B.MP

   Thus, the 'weakest' principles are Bishop.WLPO and Bishop.MP -- this justifies
    the definition of NonConstructive at the bottom of the file, and means that
    all the principles in Omniscience.Principles are therefore NonConstructive.

   Based on:
    Separating Fragments of WLEM, LPO, and MP by Matt Hendtlass and Robert Lubarsky
    (http://math.fau.edu/lubarsky/Separating%20LLPO.pdf)


   The double negation of any LEM-inst also holds, and etc. by the impications above.
-}


-- The first row: LEM â‡” WLEM Ã— DNE
-- Note that we actually have something stronger: âˆ€ A â†’ LEM-inst A â‡” WLEM-inst A Ã— DNE-inst A

LEMâ†’WLEM-inst : âˆ€ (A : Set â„“) â†’ LEM-inst A â†’ WLEM-inst A
LEMâ†’WLEM-inst A LEM-A = map-+ DNE-conv id LEM-A

LEMâ†’DNE-inst : âˆ€ (A : Set â„“) â†’ LEM-inst A â†’ DNE-inst A
LEMâ†’DNE-inst A (inl a) Â¬Â¬a = a
LEMâ†’DNE-inst A (inr Â¬a) Â¬Â¬a = absurd (Â¬Â¬a Â¬a)

WLEMÃ—DNEâ†’LEM-inst : âˆ€ (A : Set â„“) â†’ WLEM-inst A â†’ DNE-inst A â†’ LEM-inst A
WLEMÃ—DNEâ†’LEM-inst A WLEM-A DNE-A = map-+ DNE-A id WLEM-A
                                -- notice the similarity to LEMâ†’WLEM-inst!

LEMâ†’WLEM : LEM â†’ WLEM
LEMâ†’DNE  : LEM â†’ DNE
LEMâ†’WLEM lem A = LEMâ†’WLEM-inst A (lem A)
LEMâ†’DNE  lem A = LEMâ†’DNE-inst A (lem A)
WLEMÃ—DNEâ†’LEM : WLEM â†’ DNE â†’ LEM
WLEMÃ—DNEâ†’LEM wlem dne A = WLEMÃ—DNEâ†’LEM-inst A (wlem A) (dne A)


-- The second row: LEM â‡” LPO , WLEM â‡” WLPO , DNE â‡” MP
-- Note that we actually have stronger relations between LEM-inst & LPO-inst, etc.

LEMâ†’LPO-inst : âˆ€ (A : Set â„“a) (P : A â†’ Set â„“p) â†’ LEM-inst (âˆƒ A P) â†’ LPO-inst A P
LEMâ†’LPO-inst A P LEM-âˆƒAP = map-+ id (fwd âˆƒ-DeM) LEM-âˆƒAP

LPOâ†’LEM-inst : âˆ€ (A : Set â„“a) â†’ LPO-inst âŠ¤ (const A) â†’ LEM-inst A
LPOâ†’LEM-inst A LPO-âŠ¤-A = map-+ snd (Î» f â†’ f tt) LPO-âŠ¤-A

WLEMâ†’WLPO-inst : âˆ€ (A : Set â„“a) (P : A â†’ Set â„“p) â†’ WLEM-inst (âˆƒ A P) â†’ WLPO-inst A P
WLEMâ†’WLPO-inst A P WLEM-âˆƒAP = map-+ (fwd-contra (bwd âˆƒ-DeM)) (fwd âˆƒ-DeM) WLEM-âˆƒAP

WLPOâ†’WLEM-inst : âˆ€ (A : Set â„“a) â†’ WLPO-inst âŠ¤ (const A) â†’ WLEM-inst A
WLPOâ†’WLEM-inst A WLPO-âŠ¤-A = map-+ (Î» f Â¬a â†’ f (const Â¬a)) (Î» f â†’ f tt) WLPO-âŠ¤-A

DNEâ†’MP-inst : âˆ€ (A : Set â„“a) (P : A â†’ Set â„“p) â†’ DNE-inst (âˆƒ A P) â†’ MP-inst A P
DNEâ†’MP-inst A P DNE-âˆƒAP = DNE-âˆƒAP âˆ˜ fwd-contra (fwd âˆƒ-DeM)

MPâ†’DNE-inst : âˆ€ (A : Set â„“a) â†’ MP-inst âŠ¤ (const A) â†’ DNE-inst A
MPâ†’DNE-inst A MP-âŠ¤-A = snd âˆ˜ MP-âŠ¤-A âˆ˜ (Î» Â¬Â¬a f â†’ Â¬Â¬a (f tt))

LEMâ†’LPO   : LEM â†’ âˆ€ {â„“} (A : Set â„“) â†’ LPO A
WLEMâ†’WLPO : WLEM â†’ âˆ€ {â„“} (A : Set â„“) â†’ WLPO A
DNEâ†’MP    : DNE â†’ âˆ€ {â„“} (A : Set â„“) â†’ MP A
LEMâ†’LPO   lem  A P = LEMâ†’LPO-inst A P (lem (âˆƒ A P))
WLEMâ†’WLPO wlem A P = WLEMâ†’WLPO-inst A P (wlem (âˆƒ[ a âˆ¶ A ] P a))
DNEâ†’MP    dne  A P = DNEâ†’MP-inst A P (dne (âˆƒ[ a âˆ¶ A ] P a))
LPOâ†’LEM   : (âˆ€ {â„“} (A : Set â„“) â†’ LPO A) â†’ LEM
WLPOâ†’WLEM : (âˆ€ {â„“} (A : Set â„“) â†’ WLPO A) â†’ WLEM
MPâ†’DNE    : (âˆ€ {â„“} (A : Set â„“) â†’ MP A) â†’ DNE
LPOâ†’LEM   lpo  A = LPOâ†’LEM-inst A (lpo âŠ¤ (const A))
WLPOâ†’WLEM wlpo A = WLPOâ†’WLEM-inst A (wlpo âŠ¤ (const A))
MPâ†’DNE    mp   A = MPâ†’DNE-inst A (mp âŠ¤ (const A))


-- The third row: LPO â‡” WLPO Ã— MP
-- Note that we actually have something stronger: âˆ€ A P â†’ LPO-inst A P â‡” WLPO-inst A P Ã— MP-inst A P
-- (Notice the similarities with the proofs of the first row!)

LPOâ†’WLPO-inst : âˆ€ (A : Set â„“) (P : A â†’ Set â„“') â†’ LPO-inst A P â†’ WLPO-inst A P
LPOâ†’WLPO-inst A P LPO-A-P = map-+ ((fwd-contra (bwd âˆƒ-DeM)) âˆ˜ DNE-conv) id (LPO-A-P)

LPOâ†’MP-inst : âˆ€ (A : Set â„“) (P : A â†’ Set â„“') â†’ LPO-inst A P â†’ MP-inst A P
LPOâ†’MP-inst A P LPO-A-P Â¬f with LPO-A-P
... | inl (a , p) = (a , p)
... | inr f = absurd (Â¬f f)

WLPOÃ—MPâ†’LPO-inst : âˆ€ {â„“} (A : Set â„“) (P : A â†’ Set â„“') â†’ WLPO-inst A P â†’ MP-inst A P â†’ LPO-inst A P
WLPOÃ—MPâ†’LPO-inst A P WLPO-A-P MP-A-P = map-+ (MP-A-P) id (WLPO-A-P)

LPOâ†’WLPO : âˆ€ (A : Set â„“) â†’ LPO A â†’ WLPO A
LPOâ†’MP   : âˆ€ (A : Set â„“) â†’ LPO A â†’ MP A
LPOâ†’WLPO A LPO-A P = LPOâ†’WLPO-inst A P (LPO-A P)
LPOâ†’MP   A LPO-A P = LPOâ†’MP-inst   A P (LPO-A P)
WLPOÃ—MPâ†’LPO : âˆ€ (A : Set â„“) â†’ WLPO A â†’ MP A â†’ LPO A
WLPOÃ—MPâ†’LPO A WLPO-A MP-A P = WLPOÃ—MPâ†’LPO-inst A P (WLPO-A P) (MP-A P)


-- The fourth row: LPO â†’ B.LPO , WLPO â†’ B.WLPO , MP â†’ B.MP

LPOâ†’BishopLPO : âˆ€ {A : Set â„“} â†’ LPO A â†’ Bishop.LPO A
LPOâ†’BishopLPO LPO-A P = LPO-A (Î» a â†’ isTrue (P a))

WLPOâ†’BishopWLPO : âˆ€ {A : Set â„“} â†’ WLPO A â†’ Bishop.WLPO A
WLPOâ†’BishopWLPO WLPO-A P = WLPO-A (Î» a â†’ isTrue (P a))

MPâ†’BishopMP : âˆ€ {A : Set â„“} â†’ MP A â†’ Bishop.MP A
MPâ†’BishopMP MP-A P = MP-A (Î» a â†’ isTrue (P a))


-- The fifth row: Bishop.LPO â‡” Bishop.WLPO Ã— Bishop.MP
-- (Idential to the proofs of the second row)

BishopLPOâ†’BishopWLPO : âˆ€ {A : Set â„“} â†’ Bishop.LPO A â†’ Bishop.WLPO A
BishopLPOâ†’BishopWLPO LPO-A P = map-+ ((fwd-contra (bwd âˆƒ-DeM) âˆ˜ DNE-conv)) id (LPO-A P)

BishopLPOâ†’BishopMP : âˆ€ {A : Set â„“} â†’ Bishop.LPO A â†’ Bishop.MP A
BishopLPOâ†’BishopMP LPO-A P Â¬f with LPO-A P
... | inl (a , p) = (a , p)
... | inr f = absurd (Â¬f f)

BishopWLPOÃ—MPâ†’BishopLPO : âˆ€ {A : Set â„“} â†’ Bishop.WLPO A â†’ Bishop.MP A â†’ Bishop.LPO A
BishopWLPOÃ—MPâ†’BishopLPO WLPO-A MP-A P = map-+ (MP-A P) id (WLPO-A P)


-- The double negation of LEM-inst...

Â¬Â¬LEM-inst : âˆ€ (A : Set â„“) â†’ Â¬Â¬ (LEM-inst A)
Â¬Â¬LEM-inst A Â¬LEM = Â¬LEM (inr (Î» a â†’ Â¬LEM (inl a)))

-- ... and its implications

Â¬Â¬WLEM-inst : âˆ€ (A : Set â„“) â†’ Â¬Â¬ (WLEM-inst A)
Â¬Â¬WLEM-inst A = assume-dn (Â¬Â¬LEM-inst A) (LEMâ†’WLEM-inst A)

Â¬Â¬DNE-inst : âˆ€ (A : Set â„“) â†’ Â¬Â¬ (DNE-inst A)
Â¬Â¬DNE-inst A = assume-dn (Â¬Â¬LEM-inst A) (LEMâ†’DNE-inst A)

Â¬Â¬LPO-inst : âˆ€ (A : Set â„“a) (P : A â†’ Set â„“p) â†’ Â¬Â¬ (LPO-inst A P)
Â¬Â¬LPO-inst A P = assume-dn (Â¬Â¬LEM-inst (âˆƒ A P)) (LEMâ†’LPO-inst A P)

Â¬Â¬WLPO-inst : âˆ€ (A : Set â„“a) (P : A â†’ Set â„“p) â†’ Â¬Â¬ (WLPO-inst A P)
Â¬Â¬WLPO-inst A P = assume-dn (Â¬Â¬WLEM-inst (âˆƒ A P)) (WLEMâ†’WLPO-inst A P)

Â¬Â¬MP-inst : âˆ€ (A : Set â„“a) (P : A â†’ Set â„“p) â†’ Â¬Â¬ (MP-inst A P)
Â¬Â¬MP-inst A P = assume-dn (Â¬Â¬DNE-inst (âˆƒ A P)) (DNEâ†’MP-inst A P)

Â¬Â¬BishopLPO-inst : âˆ€ (A : Set â„“) (P : A â†’ ğŸš) â†’ Â¬Â¬ (Bishop.LPO-inst A P)
Â¬Â¬BishopLPO-inst A P = assume-dn (Â¬Â¬LPO-inst A (Î» a â†’ isTrue (P a))) id

Â¬Â¬BishopWLPO-inst : âˆ€ (A : Set â„“) (P : A â†’ ğŸš) â†’ Â¬Â¬ (Bishop.WLPO-inst A P)
Â¬Â¬BishopWLPO-inst A P = assume-dn (Â¬Â¬WLPO-inst A (Î» a â†’ isTrue (P a))) id

Â¬Â¬BishopMP-inst : âˆ€ (A : Set â„“) (P : A â†’ ğŸš) â†’ Â¬Â¬ (Bishop.MP-inst A P)
Â¬Â¬BishopMP-inst A P = assume-dn (Â¬Â¬MP-inst A (Î» a â†’ isTrue (P a))) id
